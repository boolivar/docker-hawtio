plugins {
    id "base"
    id "com.bmuschko.docker-remote-api" version "6.7.0"
}

repositories {
    mavenCentral()
}

configurations {
    hawtio {
        canBeConsumed = false
        canBeResolved = false
        transitive = false
    }
}

dependencies {
    hawtio "io.hawt:hawtio-jbang:3.0.0"
}

docker {
    registryCredentials {
        username = dockerhubUser
        password = dockerhubPass
    }
}

version configurations.hawtio.dependencies.version[0]

final image = "boolivar/hawtio:$version"

tasks.named("build") {
    dependsOn "dockerBuildImage"
}

tasks.register("dockerBuildImage", com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
    inputDir = rootDir
    images = [image]
    buildArgs = [HAWTIO_VERSION: "hawtio-$version", JAVA_VERSION: javaVersion]
}

tasks.register("dockerPushImage", com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
    images = [image]
}

tasks.register("version") {
    doLast {
        print version
    }
}