plugins {
    id "base"
    id "com.bmuschko.docker-remote-api" version "10.0.0"
}

repositories {
    mavenCentral()
}

configurations {
    hawtio {
        canBeConsumed = false
        canBeResolved = false
        transitive = false
    }
}

dependencies {
    hawtio "io.hawt:hawtio-jbang:4.6.0"
}

docker {
    registryCredentials {
        username = dockerhubUser
        password = dockerhubPass
    }
}

version configurations.hawtio.dependencies.version[0]

ext {
    testContainerName = "docker-hawtio-test"
    imageList = (tags ? tags.split(",") : [version]).collect { "boolivar/hawtio:${it.trim()}" }
}

tasks.register("version") {
    doLast {
        print version
    }
}

tasks.register("dockerBuildImage", com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
    inputDir = rootDir
    images = imageList
    buildArgs = [HAWTIO_VERSION: version, JAVA_VERSION: javaVersion]
    labels = [
            "org.opencontainers.image.version": version,
            "org.opencontainers.image.created": Instant.now().truncatedTo(java.time.temporal.ChronoUnit.MILLIS).toString()
        ].plus(currentRevision ? ["org.opencontainers.image.revision": currentRevision] : [:])
}

tasks.register("dockerPushImage", com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
    images = imageList
}

tasks.register("createContainer", com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer) {
    containerName = testContainerName
    mustRunAfter dockerBuildImage
    targetImageId imageList[0]
    cmd = ["--host=0.0.0.0"]
    hostConfig.with {
        portBindings = ["8080:8080"]
        autoRemove = true
    }
}

tasks.register("startContainer", com.bmuschko.gradle.docker.tasks.container.DockerStartContainer) {
    dependsOn createContainer
    containerId = testContainerName
}

tasks.register("stopContainer", com.bmuschko.gradle.docker.tasks.container.DockerStopContainer) {
    containerId = testContainerName
}

tasks.register("testContainer") {
    dependsOn startContainer
    finalizedBy stopContainer
    doLast {
        def error = null
        for (int i = 0; i < 10; ++i) {
            try {
                assert new URL("http://localhost:8080/hawtio").openConnection().responseCode == 200
                return
            } catch (Exception e) {
                error = e
                Thread.sleep(1000)
            }
        }
        throw error
    }
}

tasks.named("assemble") {
    dependsOn dockerBuildImage
}

tasks.named("check") {
    dependsOn testContainer
}
